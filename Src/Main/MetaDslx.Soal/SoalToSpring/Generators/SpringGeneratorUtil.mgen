namespace MetaDslx.Soal;
generator SpringGeneratorUtil for object;



properties Properties
	string entityPackage = "entity";
	string repositoryPackage = "repository";
	string exceptionPackage = "exception";
	string interfacePackage = "interfaces";
	string enumPackage = "enums";
	string serviceFacadePackage = "facade";
	string controllerPackage = "controller";
	string proxyPackage = "proxy";
end properties



template GenerateGetter(Property prop)
^
public [prop.Type.GetJavaName()] get[prop.Name.ToPascalCase()]() {
	return this.[prop.Name.ToCamelCase()];
}
end template



template GenerateSetter(Property prop)
^
public void set[prop.Name.ToPascalCase()]([prop.Type.GetJavaName()] [prop.Name.ToCamelCase()]) {
	this.[prop.Name.ToCamelCase()] = [prop.Name.ToCamelCase()];
}
end template



template GenerateImports(Declaration declaration)
[GenerateImports(declaration, false, "")]
end template



template GenerateImports(Declaration declaration, bool reposNeeded, string dataBinding)
	[var imports = declaration.GetImports(dataBinding)]
	[if (imports.Any())]
		[loop (import : imports)]
			[if ( !(import.Contains("repo") && reposNeeded==false ))]
[import]
			[end if]
		[end loop]
^
	[end if]
end template



function string GetParameterList(Operation op)
    string result = "";
    loop (op->param:Parameters; string delimiter = ""; delimiter = ", ")
        result = result + delimiter + param.Type.GetJavaName() + " " + param.Name.ToCamelCase();
    end loop
    return result;
end function



function string GetParameterNameList(Operation op)
    string result = "";
    loop (op->param:Parameters; string delimiter = ""; delimiter = ", ")
        result = result + delimiter + param.Name.ToCamelCase();
    end loop
    return result;
end function



function string GetParameterTypeList(Operation op)
    string result = "";
    loop (op->param:Parameters; string delimiter = ", ")
		string javaName = param.Type.GetJavaName();
		if (javaName.Contains("<"))
			javaName = "Object";
		end if
        result = result + delimiter + javaName + ".class";
    end loop
    return result;
end function



function string GetExceptionList(Operation op)
    string result = "";
    loop (op->exc:Exceptions; string delimiter = ""; delimiter = ", ")
        result = result + delimiter + exc.GetJavaName();
    end loop
    return result;
end function



function string GetPropertyList(Struct sType)
    string result = "";
    loop (sType->p:Properties; string delimiter = ""; delimiter = ", ")
        result = result + delimiter + p.Type.GetJavaName() + " " + p.Name.ToCamelCase();
    end loop
    return result;
end function



function int GetNumberOfFieldWithIdSuffix(Struct sType)
	int result = 0;
	loop (sType->p:Properties)
		if (p.Name.EndsWith("Id"))
			result++;
		end if
    end loop
    return result;
end function



function string GetPackage(Declaration d)
	return d.Namespace.FullName.ToLower();
end function



function string GetBindingType(Reference reference)
	Binding binding = reference.Binding;
	if (binding == null)
		return "";
	end if
	if (binding.Transport is RestTransportBindingElement)
		return "Rest";
	end if
	if (binding.Transport is WebSocketTransportBindingElement)
		return "WebSocket";
	end if
	loop (binding->encoding:Encodings)
		if (encoding is SoapEncodingBindingElement)
			return "WebService";
		end if
	end loop
	return "";
end function
