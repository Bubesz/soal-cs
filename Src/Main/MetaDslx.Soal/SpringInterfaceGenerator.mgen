namespace MetaDslx.Soal;
generator SpringInterfaceGenerator for object;

using generator SpringGeneratorUtil;


template GenerateInterface(Interface iface, string bindingType)
package [SpringGeneratorUtil.GetPackage(iface)].[SpringGeneratorUtil.Properties.interfacePackage];
^
[if (bindingType.Equals("Rest"))]
import org.springframework.web.bind.annotation.PathVariable;
import org.springframework.web.bind.annotation.RequestMapping;
import org.springframework.web.bind.annotation.RestController;
[end if]
[if (bindingType.Equals("WebService"))]
//import ?;
[end if]
[if (bindingType.Equals("WebSockett"))]
//import ?;
[end if]

[SpringGeneratorUtil.GenerateImports(iface, false)]
^
[InterfaceAnnotation(bindingType)]
public interface [iface.Name][bindingType] {
[loop (iface->op:Operations)]
^
[OperationAnnotation(bindingType, op)]
	[op.Result.Type.GetJavaName()] [op.Name.ToCamelCase()](\
	[loop (op->param:Parameters; string delimiter = ""; delimiter = ", ")]
[delimiter][ParameterAnnotation(bindingType)][param.Type.GetJavaName()] [param.Name.ToString().ToCamelCase()]\
    [end loop]
)\
	[if (op.Exceptions.Any())]
 throws [SpringGeneratorUtil.GetExceptionList(op)]\
	[end if]
;

[end loop]
}
end template



template GenerateInterfaceImplementation(Interface iface, string functionName)
package [SpringGeneratorUtil.GetPackage(iface)].[functionName];
^
import org.springframework.beans.factory.annotation.Autowired;
import org.springframework.stereotype.Service;
^
import [SpringGeneratorUtil.GetPackage(iface)].[SpringGeneratorUtil.Properties.interfacePackage].[iface.Name];
[SpringGeneratorUtil.GenerateImports(iface)]
^
@Service
public class [iface.Name]Impl implements [iface.Name] {
[loop (iface->repo:GetRepositories())]
^
	@Autowired
	private [repo];
[end loop]
[loop(iface->op:Operations)]
^
	public [op.Result.Type.GetJavaName()] [op.Name.ToCamelCase()]([SpringGeneratorUtil.GetParameterList(op)])\
	[if (op.Exceptions.Any())]
 throws [SpringGeneratorUtil.GetExceptionList(op)] \
	[end if]
{
		// TODO implement method
		throw new UnsupportedOperationException("Not yet implemented.");
	}
[end loop]
}
end template



template GenerateProxyInterfaceImplementation(Interface iface, string functionName, string bindingType)
package [SpringGeneratorUtil.GetPackage(iface)].[functionName];
^
import org.springframework.beans.factory.annotation.Autowired;
import org.springframework.stereotype.Service;
^
import [SpringGeneratorUtil.GetPackage(iface)].[SpringGeneratorUtil.Properties.interfacePackage].[iface.Name][bindingType];
[SpringGeneratorUtil.GenerateImports(iface, false)]
^
@Service
public class [iface.Name][bindingType]Impl implements [iface.Name][bindingType] {
^
	@Autowired
	private [iface.Name]Impl [iface.Name.ToCamelCase()]Impl;

[loop(iface->op:Operations)]
^
	public [op.Result.Type.GetJavaName()] [op.Name.ToCamelCase()]([SpringGeneratorUtil.GetParameterList(op)])\
	[if (op.Exceptions.Any())]
 throws [SpringGeneratorUtil.GetExceptionList(op)] \
	[end if]
{
			[if (op.Result.Type.GetJavaName() != "void")]
		return [iface.Name.ToCamelCase()]Impl.[op.Name.ToCamelCase()]([SpringGeneratorUtil.GetParameterNameList(op)]);
			[else]
		[iface.Name.ToCamelCase()]Impl.[op.Name.ToCamelCase()]([SpringGeneratorUtil.GetParameterNameList(op)]);
			[end if]
	}
[end loop]
}
end template



template InterfaceAnnotation(string bindingType)
	[if (bindingType.Equals("Rest"))]
@RestController
	[else if (bindingType.Equals("WebService"))]
//@WebService
	[else if (bindingType.Equals("WebSockett"))]
//@WebSocket
	[end if]
end template



template OperationAnnotation(string bindingType, Operation op)
	[if (bindingType.Equals("Rest"))]
	@RequestMapping(value="[op.Name.ToCamelCase()]")
	[else if (bindingType.Equals("WebService"))]
	//@WebService
	[else if (bindingType.Equals("WebSockett"))]
	//@WebSocket
	[end if]
end template



template ParameterAnnotation(string bindingType)
	[if (bindingType.Equals("Rest"))]
@PathVariable \
	[else if (bindingType.Equals("WebService"))]
		
	[else if (bindingType.Equals("WebSockett"))]
		
	[end if]
end template
